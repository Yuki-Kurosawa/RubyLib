apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdk 33

    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'yuki.control.extended.webviewex'
    buildToolsVersion '34.0.0'
}


def versionName = MVNVERSION
def SNAPSHOT_REPOSITORY_URL="https://s01.oss.sonatype.org/content/repositories/snapshots/"
def RELEASE_REPOSITORY_URL="https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

publishing {
    publications {
        // 定义一个名为 Production 的发布
        Production(MavenPublication) {
            // 使用方引用 implementation 'cn.com.jack:mavendemo:2.7.0-SNAPSHOT'
            groupId = "app.ksyuki"
            artifactId = "test"
            version = versionName
            // 必须有这个 否则不会上传AAR包
            afterEvaluate { artifact(tasks.getByName("bundleReleaseAar")) }
            // 上传source，这样使用方可以看到方法注释


            pom {
                name = 'Yuki Library - Maven Test'
                description = 'Maven Test'
                url = 'https://github.com/Takeya-Yuki/RubyLib.git'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'Yuki Kurosawa'
                        name = 'Yuki Kurosawa'
                        email = 'yuki@yuki.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Takeya-Yuki/RubyLib.git'
                    developerConnection = 'scm:git:ssh://github.com/Takeya-Yuki/RubyLib.git'
                    url = 'https://github.com/Takeya-Yuki/RubyLib.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = versionName.endsWith('SNAPSHOT') ? SNAPSHOT_REPOSITORY_URL : RELEASE_REPOSITORY_URL
            credentials {
                username = MVNUSER
                password = MVNPASS
            }
        }
    }
}


signing {
    sign publishing.publications
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.5.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
}
